=pod

=head1 NAME

Moose::Manual::Classes - Fazendo suas classes utilizar Moose ( e subclasse )

=head1 USING MOOSE

Usar Moose é muito simples, você só utiliza C<use Moose>:

  package Person;

  use Moose;

É isso aí, você fez uma classe com Moose!

Existe muita coisa acontecendo aqui sob o capô, então vamos analisar-lo.

Quando você carrega L<Moose>, um monte de facilidades são exportados 
para sua classe. Estas incluem coisas como C<extends>, C<has>, C<with> 
e muito mais. Essas funções são o que você usa para definir sua classe. 
Por exemplo, para definir um atributo ...

  package Person;

  use Moose;

  has 'ssn' => ( is => 'rw' );

Atributos são descritos na documentação L<Moose::Manual::Attributes>.

Carregar o Moose também habilita os pragmas C<strict> e C<warnings> 
na sua classe.

Quando você carrega o Moose, sua classe se tornará uma subclasse do
L<Moose::Object>. A classe L<Moose::Object> provê o construtor e 
destrutor padrão, bem como os métodos de construção de objeto.
Você pode lêr mais sobre isto no documento L<Moose::Manual::Construction>.

Por conveniência, Moose cria um novo tipo de classe para sua classe. Veja 
o documento L<Moose::Manual::Types> para saber mais sobre os tipos.

Ele também cria um objeto L<Moose::Meta::Class> para sua classe. Este
objeto metaclasse está disponível através do método C<meta> na sua classe,
por exemplo C<< Person->meta>>.

O objeto metaclasse fornece API de introspeção para sua classe. Ele 
também é usado pelo próprio Moose para adicionar atributos, definir 
classes pai, e assim por diante. Na verdade, todas as facilidades do Moose 
utilizam estes métodos do objeto metaclasse (e outras meta API).

=head1 SUBCLASSES

Moose providência uma função simples para a declaração de suas
classes pai, o C<extends>:

  package User;

  use Moose;

  extends 'Person';

  has 'username' => ( is => 'rw' );

Note that each call to C<extends> will I<reset> your parents. For
multiple inheritance you must provide all the parents at once,
C<extends 'Foo', 'Bar'>.

You can use Moose to extend a non-Moose parent. However, when you do
this, you will inherit the parent class's constructor (assuming it is
also called C<new>). In that case, you will have to take care of
initializing attributes manually, either in the parent's constructor,
or in your subclass, and you will lose a lot of Moose magic.

=head1 NO MOOSE

Moose also allows you to remove its sugar functions from your class's
namespace. We recommend that you take advantage of this feature, since
it just makes your classes "cleaner". You can do this by simply adding
C<no Moose> at the end of your module file.

  package Person;

  use Moose;

  has 'ssn' => ( is => 'rw' );

  no Moose;

This deletes Moose's sugar functions from your class's namespace, so
that C<< Person->can('has') >> will no longer return true.

A more generic way to unimport not only L<Moose>'s exports but also
those from type libraries and other modules is to use
L<namespace::clean> or L<namespace::autoclean>.

=head1 MAKING IT FASTER

Moose has a feature called "immutabilization" that you can use to
greatly speed up your classes at runtime. However, using it does incur
a cost when your class is first being loaded. When you make your class
immutable you tell Moose that you will not be changing it in the
future. You will not be adding any more attributes, methods, roles, etc.

This allows Moose to generate code specific to your class. In
particular, it creates an "inline" constructor, making object
construction much faster.

To make your class immutable you simply call C<make_immutable> on your
class's metaclass object.

  __PACKAGE__->meta->make_immutable;

=head2 Immutabilization and C<new()>

If you override C<new()> in your class, then the immutabilization code
will not be able to provide an optimized constructor for your
class. Instead, you should use a C<BUILD()> method, which will be
called from the inlined constructor.

Alternately, if you really need to provide a different C<new()>, you
can also provide your own immutabilization method. Doing so requires
extending the Moose metaclasses, and is well beyond the scope of this
manual.

=head1 AUTHOR

Dave Rolsky E<lt>autarch@urth.orgE<gt>

=head1 COPYRIGHT AND LICENSE

Copyright 2008-2009 by Infinity Interactive, Inc.

L<http://www.iinteractive.com>

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=cut
